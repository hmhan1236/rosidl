# generated from
# rosidl_cmake/cmake/template/rosidl_cmake_export_typesupport_targets.cmake.in

set(_exported_typesupport_targets
  "@_ROSIDL_CMAKE_EXPORT_TYPESUPPORT_TARGETS@")

# populate @PROJECT_NAME@_TARGETS_<suffix>
if(NOT _exported_typesupport_targets STREQUAL "")
  set(_exported_targets_reversed ${_exported_typesupport_targets})
  list(REVERSE _exported_targets_reversed)
  foreach(_tuple ${_exported_targets_reversed})
    string(REPLACE ":" ";" _tuple "${_tuple}")
    list(GET _tuple 0 _suffix)
    list(GET _tuple 1 _target)
    list(GET _tuple 2 _single_typesupport)

    set(_export_file "${@PROJECT_NAME@_DIR}/${_target}Export.cmake")
    if(NOT ${_single_typesupport} STREQUAL "TRUE")
      # Already include by ament_export_targets-extras.cmake otherwise
      include("${_export_file}")
    endif()

    # extract the target names associated with the export
    set(_regex "foreach\\(_expectedTarget (.+)\\)")
    file(
      STRINGS "${_export_file}" _foreach_targets
      REGEX "${_regex}")
    list(LENGTH _foreach_targets _matches)
    if(NOT _matches EQUAL 1)
      message(FATAL_ERROR
        "Failed to find exported target names in '${_export_file}'")
    endif()
    string(REGEX REPLACE "${_regex}" "\\1" _targets "${_foreach_targets}")
    string(REPLACE " " ";" _targets "${_targets}")
    list(LENGTH _targets _length)

    list(APPEND @PROJECT_NAME@_TARGETS${_suffix} ${_targets})
  endforeach()
endif()
